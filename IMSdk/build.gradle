plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'com.jfrog.artifactory'
}

apply from: "../module.build.gradle"

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "String", "WS_HOST", RELEASE_WS_API
            buildConfigField "String", "SERVICE_NAME", RELEASE_IM_SERVICE_NAME 
            buildConfigField "String", "HOST", RELEASE_IM_HOST
            buildConfigField "String", "DB_NAME_PREFIX", RELEASE_DB_NAME_PREFIX

            buildConfigField "String", "VERSION_NAME", SDK_VERSION_NAME
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            buildConfigField "String", "WS_HOST", DEBUG_WS_API 
            buildConfigField "String", "SERVICE_NAME", DEBUG_IM_SERVICE_NAME 
            buildConfigField "String", "HOST", DEBUG_IM_HOST
            buildConfigField "String", "DB_NAME_PREFIX", DEBUG_DB_NAME_PREFIX

            buildConfigField "String", "VERSION_NAME", SDK_VERSION_NAME
        }

        create("dev") {
            debuggable true
            minifyEnabled false
            buildConfigField "String", "WS_HOST", DEV_WS_API
            buildConfigField "String", "SERVICE_NAME", DEV_IM_SERVICE_NAME
            buildConfigField "String", "HOST", DEV_IM_HOST
            buildConfigField "String", "DB_NAME_PREFIX", DEV_DB_NAME_PREFIX

            buildConfigField "String", "VERSION_NAME", SDK_VERSION_NAME
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }
}

kapt {
    correctErrorTypes = true
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
//        arg("AROUTER_GENERATE_DOC", "enable")
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def roomVersion = "2.4.3"
    api "androidx.room:room-runtime:${roomVersion}"
    kapt "androidx.room:room-compiler:${roomVersion}"
    api "androidx.room:room-ktx:${roomVersion}"
    api "androidx.room:room-testing:${roomVersion}"

    api 'androidx.recyclerview:recyclerview:1.3.0-beta02'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'com.google.android.material:material:1.6.1'

    api 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.0'
//    api 'androidx.lifecycle:lifecycle-common-java8:2.4.1'
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0"

    api 'org.jetbrains.kotlin:kotlin-stdlib:1.7.10'

    api 'com.google.code.gson:gson:2.9.0'

    api 'com.blankj:utilcodex:1.30.6'

    api 'id.zelory:compressor:3.0.1'

    api "com.tencent:mmkv-static:1.2.1"

    api "io.github.cymchad:BaseRecyclerViewAdapterHelper:4.0.0-beta04"

    api 'com.squareup.okhttp3:okhttp:4.10.0'
    api 'org.greenrobot:eventbus:3.2.0'
    api('com.github.ihsanbal:LoggingInterceptor:3.1.0') {
        exclude group: 'org.json', module: 'json'
    }

//    implementation 'org.apache.logging.log4j:log4j-api:2.14.0'
//    implementation 'org.apache.logging.log4j:log4j-core:2.14.0'

}



def packageName = 'com.im'
def libraryVersion = '1.0.3'

publishing {
    publications {
        aar(MavenPublication) {
            groupId packageName
            version = libraryVersion
            artifactId project.getName()

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each{
                    // 如果有compile fileTree()，group会为空，需要去除
                    if(it.group != null) {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}